import org.jetbrains.kotlin.gradle.tasks.FatFrameworkTask

buildscript {
    repositories {
        mavenLocal()
        google()
        mavenCentral()
        jcenter()
        maven { url 'https://dl.bintray.com/kotlin/kotlin-dev' }
        maven { url 'https://dl.bintray.com/kotlin/kotlin-eap' }
        maven { url "https://dl.bintray.com/jetbrains/kotlin-native-dependencies" }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-allopen:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-frontend-plugin:0.0.37"
        classpath group: 'net.rubygrapefruit', name: 'native-platform', version: '0.21'
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4-jetbrains-3"
    }
}

plugins {
    id 'org.jetbrains.kotlin.multiplatform' version '1.3.41'
}

apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

repositories {
    mavenCentral()
}

group project.groupId
version project.kyugaVersion

def frameworkName = "KyugaIOS"

kotlin {

    iosX64 {
        binaries.framework {
            baseName = frameworkName
        }
    }

    iosArm64 {
        binaries.framework {
            baseName = frameworkName
        }
    }

    iosArm32 {
        binaries.framework {
            baseName = frameworkName
        }
    }

    jvm('android')

    sourceSets {
        commonMain {
            dependencies {
                implementation(kotlin("stdlib-common"))
            }
        }

        commonTest.dependencies {
            implementation "org.jetbrains.kotlin:kotlin-test-common"
            implementation "org.jetbrains.kotlin:kotlin-test-annotations-common"
        }

        androidMain {
            dependencies {
                implementation(kotlin("stdlib"))
            }
        }

        androidTest {
            kotlin {
                srcDir 'src/androidTest/kotlin'
            }
            dependencies {
                implementation "org.jetbrains.kotlin:kotlin-test-common"
                implementation "org.jetbrains.kotlin:kotlin-test-annotations-common"
                implementation "org.jetbrains.kotlin:kotlin-test-junit"
                implementation "org.jetbrains.kotlin:kotlin-test"
                implementation "org.json:json:20090211"
            }

            dependsOn commonMain
            dependsOn androidMain
        }

        iosMain.dependencies {
        }

        iosMain {}
        iosArm64Main.dependsOn iosMain
        iosArm32Main.dependsOn iosMain
        iosX64Main.dependsOn iosMain

        iosTest {}
        iosArm64Test.dependsOn iosTest
        iosArm32Test.dependsOn iosTest
        iosX64Test.dependsOn iosTest
    }
}

configurations {
    compileClasspath
}

task fatFramework(type: FatFrameworkTask) {
    // The fat framework must have the same base name as the initial frameworks.
    baseName = "$frameworkName"
    // Specify the frameworks to be merged.
    from(
            kotlin.targets.iosX64.binaries.getFramework("RELEASE") ,
            kotlin.targets.iosArm64.binaries.getFramework("RELEASE"),
            kotlin.targets.iosArm32.binaries.getFramework("RELEASE")
    )
}

apply from: rootProject.file('pom.gradle')
apply from: rootProject.file('gradle/bintray.gradle')
apply from: rootProject.file('gradle/publish.gradle')

tasks.build.dependsOn fatFramework